// osc_server_init.sco
//
// This script sets up the Minc functions that RTcmix uses in OSC server mode.
//

osc_debug = 1;

// All OSC-score-defined message handler functions are stored as function
// pointers, tagged with their OSC paths, in a Minc map.

map gMessageHandlerMap;

// handleOSCMessage() is invoked via a parser call generated by the receipt
// of an OSC message.  It looks to see if a handler has been registered for
// the given path and calls it if it is found and not null.

float handleOSCMessage(string messagePath, list messageArgs)
{
	mfunction messageHandler;
	handleStatus = 0;
	if (gMessageHandlerMap.contains(messagePath)) {
		messageHandler = gMessageHandlerMap[messagePath];
		print_if(osc_debug, "messageHandler is %z\n", messageHandler);
		if (messageHandler != 0) {
			handleStatus = messageHandler(messageArgs);
		}
	}
	print_if(osc_debug, "handleOSCMessage returning %d\n", handleStatus);
	return handleStatus;
}

// registerOSCMessageHandler() associates a Minc function pointer with a
// specific OSC message path.  The Minc function pointer must be the name
// of an already-defined Minc function.  The registered pointer may be
// replaced with another at any time.

float registerOSCMessageHandler(string messagePath, mfunction handler)
{
	gMessageHandlerMap[messagePath] = handler;
	return 0;
}

// unregisterOSCMessageHandler() removes the association of a Minc function 
// pointer with a specific OSC message path.

float unregisterOSCMessageHandler(string messagePath)
{
	return gMessageHandlerMap.remove(messagePath);
}

