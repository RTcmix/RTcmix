// osc_server_init.sco
//
// This script sets up the Minc functions that RTcmix uses in OSC server mode.
//

set_option("print_suppress_underbar=1");	// hide all internal function calls

osc_debug = 1;

// All OSC-score-defined message handler functions are stored as function
// pointers, tagged with their OSC paths, in a Minc map.

map __gMessageHandlerMap;

// The default Minc function which is called when no other handlers match.

float _defaultMsgHandler(list messageArgs)
{
	print_if(osc_debug, "built-in default handler called\n");
	return 0;
}

// Pointer to a that function (note: it will never be NULL)

__gDefaultMessageHandler = _defaultMsgHandler;

// handleOSCMessage() is invoked via a parser call generated by the receipt
// of an OSC message.  It looks to see if a handler has been registered for
// the given path and calls it if it is found and not null.

float handleOSCMessage(string messagePath, list messageArgs)
{
	mfunction messageHandler;
	handleStatus = 0;
	if (__gMessageHandlerMap.contains(messagePath)) {
		messageHandler = __gMessageHandlerMap[messagePath];
		print_if(osc_debug, "messageHandler is %z\n", messageHandler);
		if (messageHandler != 0) {
			handleStatus = messageHandler(messageArgs);
		}
	}
	else {
		handleStatus = __gDefaultMessageHandler(messageArgs);
	}
	print_if(osc_debug, "handleOSCMessage returning %d\n", handleStatus);
	return handleStatus;
}

// oscRegisterMessageHandler() associates a Minc function pointer with a
// specific OSC message path.  The Minc function pointer must be the name
// of an already-defined Minc function.  The registered pointer may be
// replaced with another at any time.

float oscRegisterMessageHandler(string messagePath, mfunction handler)
{
	__gMessageHandlerMap[messagePath] = handler;
	return 0;
}

// oscUnregisterMessageHandler() removes the association of a Minc function 
// pointer with a specific OSC message path.

float oscUnregisterMessageHandler(string messagePath)
{
	return __gMessageHandlerMap.remove(messagePath);
}

// oscRegisterDefaultMessageHandler() sets a Minc function pointer which
// will be called if no other handler is found which matches a path.

float oscRegisterDefaultMessageHandler(mfunction handler)
{
	__gDefaultMessageHandler = handler;
	return 0;
}

// oscUnregisterMessageHandler() removes the default function pointer

float oscUnregisterDefaultMessageHandler()
{
	__gDefaultMessageHandler = _defaultMsgHandler;
	return 0;
}

